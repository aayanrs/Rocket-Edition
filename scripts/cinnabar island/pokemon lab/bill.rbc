#dynamic 0x800000
#include stdmoves.rbh
#define _BILL 0x4
#define _FUJI 0x5
#define _BLAINE 0x3
#define _POKEBALL 0x6

#org @main
lock
faceplayer
compare 0x402E 0x12
if 0x4 goto @postMasterBall
checkitem 0x16F 0x1
compare LASTRESULT 0x1
if 0x4 goto @hasApricorn
checkitem 0x117 0x1
compare LASTRESULT 0x1
if 0x4 goto @hasChip
compare 0x402E 0x10
if 0x4 goto @noChip
compare 0x402E 0xE
if 0x4 goto @midMission1
compare 0x402E 0xD
if 0x1 goto @beta3Continuity

#org @beta3Continuity
msgbox @beta3Talk 0x6
setvar 0x402E 0xC
warpmuted 0xC 0x4 0xFF 0x7 0x9
release
end

#org @beta3Talk
= Welcome back, [player]!\nSo where were we..?\pLet's take it from the start.

#org @midMission1
msgbox @talk2 0x6
spriteface 0x4 FACE_RIGHT
release
end

#org @talk2
= Bill: Go pick that Apricorn in\nViridian Forest for us!\pNo time to lose!

#org @hasApricorn
msgbox @apricornTalk 0x6
checkitem 0x117 0x1
compare LASTRESULT 0x1
if 0x4 goto @hasBoth
textcolor 0x3
fanfare 0x105
preparemsg @giveApricorn
waitmsg
waitfanfare
removeitem 0x16F 0x1
closeonkeypress
pause 0x10
goto @noChip

#org @apricornTalk
= Bill: Ah, [player].\pDid you get ahold of the materials\nwe need?\p[.]\pExcellent.

#org @giveApricorn
= [player] handed over the\nPRPL Apricorn.

#org @noChip
textcolor 0x0
msgbox @noChipTalk 0x6
release
end

#org @noChipTalk
= Bill: Now go find us that Poké\nBall processing chip, [player].\pIt should be in the lab on the top\nfloor of your Rocket headquarters.\pThey'll never deny it to someone\nof your rank. Go for it!

#org @hasChip
msgbox @gotChipTalk 0x6
textcolor 0x3
fanfare 0x105
preparemsg @giveChip
waitmsg
waitfanfare
removeitem 0x117 0x1
closeonkeypress
goto @postHandover

#org @gotChipTalk
= Bill: You've got the chip,\n[player]?\p[.]\pMarvellous; give it here.

#org @giveChip
= [player] handed over the\nPoké Chip.

#org @hasBoth
textcolor 0x3
fanfare 0x105
preparemsg @giveBoth
waitmsg
waitfanfare
removeitem 0x16F 0x1
removeitem 0x117 0x1
closeonkeypress
goto @postHandover

#org @giveBoth
= [player] handed over the\nPRPL Apricorn and the Poké Chip.

#org @postMasterBall
msgbox @postMasterBallTalk 0x6
release
end

#org @postMasterBallTalk
= Bill: To Cerulean Cave!\pYou'll find it in the northwest\ncorner of Cerulean City.\pWe're all counting on you,\n[player].


// ------------------------------------------------------------------

#org @postHandover
textcolor 0x0
pause 0x20
spriteface _BILL FACE_RIGHT
pause 0x15
applymovement _BILL @stepR
waitmovement 0x0
spriteface _FUJI FACE_LEFT
spriteface _BLAINE FACE_LEFT
msgbox @talk3 0x6
pause 0x20
fadescreen 0x1
fadeout 0x0
special 0x113
setvar 0x8002 0x5
setvar 0x8003 0x3
callasm 0x802305 // movecamera
special 0x8E
movesprite 0xFF 0x5 0x3
spriteface 0xFF FACE_RIGHT
movesprite _BILL 0x7 0x2
spriteface _BILL FACE_DOWN
movesprite _BLAINE 0x9 0x3
spriteface _BLAINE FACE_LEFT
movesprite _FUJI 0x5 0x4
spriteface _FUJI FACE_RIGHT
showsprite _POKEBALL
special 0x114
sound 0xFD
checksound
sound 0x52
checksound
sound 0x1B
checksound
pause 0x10
fadein 0x0
fadescreen 0x0
textcolor 0x0
pause 0x20
msgbox @talk4 0x6
pause 0x20
applymovement _BILL @stepD
waitmovement 0x0
msgbox @talk5 0x6
fadeout 0x0
pause 0x20
applymovement _BILL @moveLeft
waitmovement 0x0
pause 0x25
msgbox @talk6 0x6
pause 0x25
spriteface _BILL FACE_DOWN
pause 0x25
msgbox @talk7 0x6
pause 0x25
playsong 0x121 0x0
applymovement _BILL @moveDown
waitmovement 0x0
spriteface _BILL FACE_LEFT
pause 0x25
msgbox @talk8 0x6
pause 0x25
spriteface _BILL FACE_RIGHT
pause 0x25
msgbox @talk9 0x6
pause 0x25
applymovement _BILL @moveUp
waitmovement 0x0
pause 0x25
msgbox @talk10 0x6
pause 0x20
spriteface _FUJI FACE_DOWN
pause 0x15
applymovement _FUJI @stepD
waitmovement 0x0
spriteface 0xFF FACE_DOWN
msgbox @fujiTalk1 0x6
pause 0x15
spriteface _BILL FACE_DOWN
pause 0x15
applymovement _BILL @stepD
waitmovement 0x0
msgbox @talk11 0x6
pause 0x20
spriteface _FUJI FACE_UP
pause 0x25
msgbox @fujiEllipses 0x6
pause 0x20
spriteface _FUJI FACE_DOWN
pause 0x15
applymovement _FUJI @moveDown
waitmovement 0x0
pause 0x20
msgbox @fujiTalk2 0x6
pause 0x20
spriteface _FUJI FACE_UP
pause 0x15
applymovement _FUJI @stepU
waitmovement 0x0
msgbox @fujiTalk3 0x6
pause 0x20
spriteface _FUJI FACE_RIGHT
pause 0x25
msgbox @fujiTalk4 0x6
pause 0x15
applymovement _FUJI @moveRight
waitmovement 0x0
pause 0x15
msgbox @fujiTalk5 0x6
pause 0x30
spriteface _FUJI FACE_DOWN
pause 0x20
msgbox @fujiTalk6 0x6
pause 0x25
spriteface _FUJI FACE_RIGHT
pause 0x15
applymovement _FUJI @stepR
waitmovement 0x0
msgbox @fujiTalk7 0x6
pause 0x15
spriteface _FUJI FACE_UP
pause 0x15
applymovement _FUJI @moveUp
waitmovement 0x0
pause 0x15
spriteface _FUJI FACE_LEFT
pause 0x15
msgbox @fujiTalk8 0x6
pause 0x20
spriteface _FUJI FACE_UP
pause 0x15
applymovement _FUJI @stepU
waitmovement 0x0
msgbox @fujiTalk9 0x6
pause 0x15
applymovement _BILL @stepD
waitmovement 0x0
msgbox @talk12 0x6
pause 0x15
applymovement _BILL @moveDown
waitmovement 0x0
spriteface _BILL FACE_RIGHT
pause 0x25
sound 0x35
hidesprite _POKEBALL
checksound
pause 0x15
spriteface _BILL FACE_LEFT
pause 0x10
spriteface 0xFF FACE_RIGHT
pause 0x15
additem 0x112 0x1
loadpointer 0x0 @ballGet
giveitem2 0x112 0x1 0x13E
closeonkeypress
fadeout 0x0
applymovement _BILL @stepL
waitmovement 0x0
textcolor 0x0
msgbox @talk13 0x6
pause 0x20
applymovement _BILL @moveRight
waitmovement 0x0
pause 0x25
msgbox @talk14 0x6
pause 0x25
spriteface _BILL FACE_LEFT
pause 0x20
msgbox @talk15 0x6
pause 0x10
applymovement _BILL @stepL
waitmovement 0x0
msgbox @talk16 0x6
pause 0x25
spriteface _BILL FACE_DOWN
pause 0x30
spriteface _BILL FACE_LEFT
pause 0x20
msgbox @talk17 0x6
pause 0x20
applymovement _BILL @moveLeft
waitmovement 0x0
pause 0x20
msgbox @talk18 0x6
playsong 0x117 0x0
setvar 0x402E 0x12
release
end


#org @stepR
#raw 0x1C
#raw ONSPOT_R
#raw 0x1C
#raw 0xFE

#org @stepD
#raw 0x1C
#raw ONSPOT_D
#raw 0x1C
#raw 0xFE

#org @stepU
#raw 0x1C
#raw ONSPOT_U
#raw 0x1C
#raw 0xFE

#org @stepL
#raw 0x1C
#raw ONSPOT_L
#raw 0x1C
#raw 0xFE

#org @talk3
= Bill: Hey!\nYou old slackers.\lWe're ready for production.

#org @talk4
= Bill: Well!\p[.]\c\h08ó\nIt's done.\pBeautiful little thing, don't you\nthink?

#org @talk5
= Now!\c\h08ó\nEnough messing about.\pHere's what we're going to do.\nListen carefully.

#org @moveLeft
#raw STEP_L
#raw 0xFE

#org @talk6
= The Pokémon we're looking for[.]\pThere's only one person in the\nworld who knows where it's\lcurrently hiding.

#org @talk7
= And that person's name[.]\c\h08ó\nis Shigeki.

#org @moveDown
#raw STEP_D
#raw 0xFE

#org @talk8
= That's right, [player].\pShigeki was the last of the\nscientists who worked with us on\lthe Mewtwo Project.

#org @talk9
= Trouble is, of course[.]\pNobody's seen the guy for about\nten years[.]\pFor all we know he could be dead;\nwe'd be none the wiser.

#org @moveUp
#raw STEP_U
#raw 0xFE

#org @talk10
= So[.]\pHow do we go about finding an old\nmissing genetic scientist without\la clue of his whereabouts?\p[.]\c\h08ó\nAny guesses?

#org @fujiTalk1
= Fuji: [.]\c\h08ó\nMewtwo would know where he is.

#org @talk11
= Bill: Bingo, old man.\nAnd why is that?

#org @fujiEllipses
= Fuji: [.]

#org @fujiTalk2
= Fuji: [.]\c\h08ó\nAh!\pWhat's the use in hiding anything\nanymore..?

#org @fujiTalk3
= When we merged our DNA together,\nthe five of us, to create Mewtwo[.]\pIt[.]\c\h08ó created some kind of psychic\nbond between us and the clone.

#org @fujiTalk4
= Who knows how it did it[.]\pBut it became clear pretty quickly\nthat Mewtwo could read our\lthoughts and emotions with\limpeccable accuracy.

#org @moveRight
#raw STEP_R
#raw 0xFE

#org @fujiTalk5
= Even when we were far away[.]\pThere was a constant mental link\nbetween each of us five scientists\land the newborn Pokémon.

#org @fujiTalk6
= We suspect that's how it realized\nso quickly that we had created it\lfor war[.]\pAnd why[.]\c\h08ó\nIt subsequently ran away[.]

#org @fujiTalk7
= Well!\pWho knows if the bond between us\nhasn't worn off after all these\lyears[.]\pI'm sure it must have.

#org @fujiTalk8
= But either way[.]\pMewtwo can point us towards\nShigeki's whereabouts.\pIt must know where he ran off to a\ndecade ago.

#org @fujiTalk9
= But[.]\c\h08ó\nBill.\pThis is Mewtwo we're talking\nabout.\pHow in the world would we..?

#org @talk12
= Bill: Ah!\c\h08ó\nHold it right there, my dear Fuji.

#org @ballGet
= [player] obtained the\nMaster Ball!

#org @talk13
= Bill: [player].\pThe Ball I just gave you is\nnothing but a more powerful\lstandard Poké Ball.\pIt cannot capture Mewtwo without\nits consent.\pBut does Mewtwo know that?\c\h08ó\nMaybe[.]\c\h08ó maybe not.

#org @talk14
= Either way, [player].\pGo to Cerulean Cave and seek out\nMewtwo.\pBattle it, and make it tell you\nwhere Shigeki is hiding.

#org @talk15
= Threaten to capture it if it\nrefuses to tell.\pIt may hate us humans, but it\nhates being kept in captivity\leven more.

#org @talk16
= Look, you're the strongest Trainer\nhere, [player].\lAnd frankly[.]\pMewtwo would never say a word to\nany of our faces.\pNot to safe its life.

#org @talk17
= So keep the Master Ball with you.\pBut don't use it yet[.]\c\h08ó\nSave it for the Pokémon we're\lreally after.

#org @talk18
= Well, then[.]\c\h08ó\nTo Cerulean Cave!\pYou'll find it in the northwest\ncorner of Cerulean City.\pWe're all counting on you,\n[player].

