#dynamic 0x800000
#include stdmoves.rbh
#define LETO 0x13
#define TREE 0xE
#define TREE2 0xD

#org @main
lock
pause 0x8
textcolor 0x0
msgbox @greetings 0x6
getplayerpos 0x8000 0x8001
compare 0x8001 0x17
if 0x1 goto @fromBottom
goto @fromTop

#org @fromBottom
clearflag 0x200
showsprite LETO
pause 0x10
applymovement LETO @approach
waitmovement 0x0
sound 0xA // jump sound
applymovement LETO @jumpDown
waitmovement 0x0
pause 0x15
spriteface 0xFF FACE_LEFT
spriteface LETO FACE_RIGHT
pause 0x10
msgbox @talk 0x6
applymovement LETO @stepR
waitmovement 0x0
msgbox @talk2 0x6
showmoney 0x0 0x0 0x0
msgbox @yesOrNo MSG_YESNO
compare LASTRESULT 0x1
if 0x1 goto @moneyCheck
goto @reject

#org @moneyCheck
checkmoney 0x3E8 0x0
compare LASTRESULT 0x1
if 0x1 goto @accept
hidemoney 0x0 0x0
msgbox @notEnoughTalk 0x6
pause 0x10
spriteface LETO FACE_DOWN
pause 0x10
applymovement LETO @noLeave1
waitmovement 0x0
sound 0xA // jump sound
applymovement LETO @noLeave2
waitmovement 0x0
hidesprite LETO
setflag 0x200
setvar 0x4041 0x1
release
end

#org @accept
sound 0x41
paymoney 0x3E8 0x0
updatemoney 0x0 0x0 0x0
checksound
pause 0x8
hidemoney 0x0 0x0
applymovement LETO @stepR
waitmovement 0x0
msgbox @yesTalk 0x6
applymovement LETO @cutStep
applymovement 0xFF @playerMove
waitmovement 0x0
pause 0x15
applymovement LETO @stepUquick
waitmovement 0x0
sound 0x79 // "slash attack"
applymovement TREE @cutting
waitmovement 0x0
hidesprite TREE
hidesprite TREE2
setflag 0x2B6
checksound
pause 0x20
spriteface LETO FACE_DOWN
pause 0x10
msgbox @goodBye 0x6
spriteface LETO FACE_UP
pause 0x15
applymovement LETO @yesLeave
waitmovement 0x0
hidesprite LETO
setflag 0x200
setvar 0x4041 0x1
release
end

#org @reject
hidemoney 0x0 0x0
msgbox @noTalk 0x6
pause 0x10
spriteface LETO FACE_DOWN
pause 0x10
applymovement LETO @noLeave1
waitmovement 0x0
sound 0xA // jump sound
applymovement LETO @noLeave2
waitmovement 0x0
hidesprite LETO
setflag 0x200
setvar 0x4041 0x1
release
end

#org @fromTop
clearflag 0x200
movesprite2 LETO 0x10 0x17
showsprite LETO
pause 0x10
applymovement LETO @approach2
waitmovement 0x0
pause 0x15
spriteface 0xFF FACE_DOWN
spriteface LETO FACE_UP
pause 0x10
msgbox @talk 0x6
applymovement LETO @stepU
waitmovement 0x0
msgbox @talk2 0x6
showmoney 0x0 0x0 0x0
msgbox @yesOrNo MSG_YESNO
compare LASTRESULT 0x1
if 0x1 goto @moneyCheck2
goto @reject2

#org @moneyCheck2
checkmoney 0x3E8 0x0
compare LASTRESULT 0x1
if 0x1 goto @accept2
hidemoney 0x0 0x0
msgbox @notEnoughTalk 0x6
pause 0x10
spriteface LETO FACE_LEFT
pause 0x10
applymovement LETO @leave2
waitmovement 0x0
hidesprite LETO
setflag 0x200
setvar 0x4041 0x1
release
end

#org @accept2
sound 0x41
paymoney 0x3E8 0x0
updatemoney 0x0 0x0 0x0
checksound
pause 0x8
hidemoney 0x0 0x0
applymovement LETO @stepU
waitmovement 0x0
msgbox @yesTalk 0x6
pause 0x15
applymovement LETO @stepUquick
waitmovement 0x0
sound 0x79 // "slash attack"
applymovement TREE @cutting
waitmovement 0x0
hidesprite TREE
hidesprite TREE2
setflag 0x2B6
checksound
pause 0x25
msgbox @goodBye 0x6
spriteface LETO FACE_LEFT
pause 0x15
applymovement LETO @leave2
waitmovement 0x0
hidesprite LETO
setflag 0x200
setvar 0x4041 0x1
release
end

#org @reject2
hidemoney 0x0 0x0
msgbox @noTalk 0x6
pause 0x10
spriteface LETO FACE_LEFT
pause 0x10
applymovement LETO @leave2
waitmovement 0x0
hidesprite LETO
setflag 0x200
setvar 0x4041 0x1
release
end

#org @stepR
#raw 0x1C
#raw ONSPOT_R
#raw 0x1C
#raw 0xFE

#org @stepU
#raw 0x1C
#raw ONSPOT_U
#raw 0x1C
#raw 0xFE

#org @greetings
= Do trees obscure the path to your\ndestiny? Do not fear, Trainer!

#org @approach
#raw STEP_D2
#raw STEP_D2
#raw STEP_D2
#raw STEP_D2
#raw STEP_D2
#raw STEP_D2
#raw 0xFE

#org @approach2
#raw STEP_R2
#raw STEP_R2
#raw STEP_R2
#raw STEP_R2
#raw STEP_R2
#raw STEP_R2
#raw STEP_R2
#raw STEP_R2
#raw 0xFE

#org @jumpDown
#raw 0x14
#raw 0xFE

#org @talk
= I am here, sir, and forever at\nyour service!\pThe one and only[.]\nLeto, master Cutter! \c\h08ó([.]almost.)

#org @talk2
= I Cut, dear Trainer, therefore I\nam!\pWhere I see distraught travellers\nundermined by trees in their path,\lmy giving heart fills with desire\lto set things right anew!\pLet me Cut for you the trees\naround Fuchsia and ease the weight\loff your burdened shoulders!

#org @yesOrNo
= [.]\c\h08ófor a modest fee of [$]1000,\nnaturally.

#org @yesTalk
= Ah! Sir, I daresay you made an\nexcellent choice!\pNow, if you'll allow me[.]

#org @cutStep
#raw STEP_R
#raw FACE_U
#raw 0xFE

#org @cutting
#raw 0x69
#raw 0xFE

#org @playerMove
#raw STEP_D
#raw FACE_U
#raw 0xFE

#org @stepUquick
#raw 0x1C
#raw ONSPOT_U
#raw 0xFE

#org @noTalk
= No?\c\h08ó\nWhy, f-fine, then.\pI can see that this region is not\nyet cultured enough to appreciate\lthe fine art of a proper Cut!\pWell, should the good sir ever\nchange his mind, I will never be\lfar away from any of this region's\lexquisite trees, rest assured!\pHmph! Good day.

#org @notEnoughTalk
= What?\nInsufficient funds?\pSir, my asking price is akin to\ngiving it away, I assure you!\pBut[.] if the uncultured folk of\nKanto find it too great an expense\lfor witnessing the finest of fine\lart, then what is a man to do?\pWell, should the good sir ever\nearn the necessary means to afford\lmy services, I will never be\lfar away from any of this region's\lexquisite trees, rest assured!\pHmph! Good day.

#org @goodBye
= Oh!\nWhat pleasure..!\pA Cut as splendid as this is a\nare sight in times like these,\leven to eyes such as mine!\pWell[.] should you ever be in need\nof my Cutting expertise again,\ldear Trainer, you can be sure to\lfind me by this region's most\lbeautiful trees.\pUntil we meet again!

#org @yesLeave
#raw STEP_U2
#raw STEP_U2
#raw STEP_U2
#raw STEP_U2
#raw STEP_U2
#raw STEP_U2
#raw STEP_U2
#raw STEP_U2
#raw 0xFE

#org @noLeave1
#raw STEP_D
#raw STEP_D
#raw FACE_U
#raw 0x1C
#raw 0x1C
#raw STEP_U2
#raw STEP_U2
#raw 0xFE

#org @noLeave2
#raw 0x15
#raw STEP_U2
#raw STEP_U2
#raw STEP_U2
#raw STEP_U2
#raw STEP_U2
#raw STEP_U2
#raw STEP_U2
#raw STEP_U2
#raw 0xFE

#org @leave2
#raw STEP_L2
#raw STEP_L2
#raw STEP_L2
#raw STEP_L2
#raw STEP_L2
#raw STEP_L2
#raw STEP_L2
#raw STEP_L2
#raw 0xFE