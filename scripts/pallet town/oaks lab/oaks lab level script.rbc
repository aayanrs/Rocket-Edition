#dynamic 0x800000
#include stdmoves.rbh
#define M_AIDE_1 0x3
#define F_AIDE 0x5
#define M_AIDE_2 0x6

#org @main
lockall
pause 0x10
sound 0x15 // exclamation
applymovement M_AIDE_1 @exclamation
applymovement F_AIDE @exclamation
waitmovement 0x0
playsong 0x11B 0x0
applymovement F_AIDE @stepR
waitmovement 0x0
textcolor 0x1
msgbox @femAideTalk 0x6
pause 0x10
applymovement M_AIDE_1 @aide1ToPlayer
waitmovement 0x0
textcolor 0x0
msgbox @aide1Talk 0x6
trainerbattle 0x3 0x25 0x0 @aide1Defeat
call POKESTEAL
applymovement M_AIDE_1 @stepD
waitmovement 0x0
textcolor 0x0
msgbox @aide1Talk2 0x6
pause 0x10
fadescreen 0x1
pause 0x5
sound 0x9
hidesprite M_AIDE_1
setflag 0x237
checksound
fadescreen 0x0
setvar 0x4016 0x11
releaseall
end

#org @stepR
#raw 0x1C
#raw ONSPOT_R
#raw 0x1C
#raw 0xFE

#org @stepD
#raw 0x1C
#raw ONSPOT_D
#raw 0x1C
#raw 0xFE

#org @femAideTalk
= It's that meddling pink-haired\nRocket again!\pWe're being robbed!

#org @aide1Talk
= You stay away from this lab,\nscoundrel!\pI'll say!\pAides like us can bring you down,\nno problem!\pBrace yourself!

#org @aide1Defeat
= Beware!\nWe'll get you one day!

#org @aide1Talk2
= I-I'm just an aide[.]\nI've done my part!\pProfessor Oak will not stand for\nthis, I'll have you know..!

#org @exclamation
#raw 0x62
#raw 0x1C
#raw 0x1C
#raw 0x1C
#raw 0x1C
#raw 0xFE

#org @aide1ToPlayer
#raw STEP_L2
#raw STEP_L2
#raw STEP_L2
#raw STEP_L2
#raw STEP_L2
#raw STEP_D2
#raw 0xFE






