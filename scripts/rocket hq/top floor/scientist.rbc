#dynamic 0x800000
#include stdmoves.rbh

#org @start
lock
checkitem 0x155 0x1
compare LASTRESULT 0x1
if 0x1 goto @postSurf
checkflag 0x292 // if Cinnabar mission received
if 0x1 goto @getSurf
checkflag 0x227
if 0x1 goto @postRed
checkflag 0x204
if 0x1 goto @postMtMoon
checkflag 0x201
if 0x0 call @receive
msgbox @talk4 0x6
end

#org @receive
msgbox @talk 0x6
applymovement 0x1 @walk
waitmovement 0x0
msgbox @talk2 0x6
applymovement 0x1 @walk2
waitmovement 0x0
msgbox @talk3 0x6
giveitem 0x154 0x1 0x0
setflag 0x201
setvar 0x4011 0x0
return

#org @talk
= Another new recuit, is it?\pYeah, I've got that HM of yours\nright here; just a sec.

#org @walk
#raw 0x10
#raw 0x10
#raw 0x13
#raw 0x13
#raw 0x1C
#raw 0x24
#raw 0xFE

#org @talk2
= Free Fly for all grunts, now, eh?\nMy, Giovanni's going soft.

#org @walk2
#raw 0x12
#raw 0x12
#raw 0x11
#raw 0x11
#raw 0xFE

#org @talk3
= Here it is, pal. Don't go putting\nit to waste, now.

#org @talk4
= Now get out of here-- I've got\nwork to do.

#org @postMtMoon
msgbox @talk5 0x6
release
end

#org @postRed
msgbox @talk6 0x6
release
end

#org @getSurf
faceplayer
msgbox @talk7 0x6
getplayerpos 0x8000 0x8001
compare 0x8000 0x7
if 0x1 goto @fromRight
applymovement 0x1 @moveToBox1
waitmovement 0x0
msgbox @talk8 0x6
applymovement 0x1 @moveBack1
waitmovement 0x0
goto @getSurf2

#org @fromRight
applymovement 0x1 @moveToBox2
waitmovement 0x0
msgbox @talk8 0x6
applymovement 0x1 @moveBack2
waitmovement 0x0
goto @getSurf2

#org @getSurf2
faceplayer
pause 0x15
giveitem 0x155 0x1 0x0
release
end

#org @postSurf
faceplayer
msgbox @talk9 0x6
release
end

#org @talk5
= (If there is anything I hate more\nthan being a supplier for rookies\lon recruitment day[.])\p(It is to be pestered by those\nvery same rookies while I'm TRYING\lTO WORK.)\p[.]\p(Maybe if I just ignore them\nthey'll go away on their own[.])

#org @talk6
= (Thank god that little kid beat\neverybody else and not just me[.])

#org @talk7
= Hm..?\nBoss told you to talk to me?\p[.]\pOh, Surf is it now?\nGood lord, in all my days[.]

#org @moveToBox1
#raw STEP_R
#raw FACE_U
#raw 0x1C
#raw ONSPOT_U
#raw 0xFE

#org @moveBack1
#raw STEP_L
#raw 0xFE

#org @moveToBox2
#raw STEP_U
#raw FACE_R
#raw 0x1C
#raw ONSPOT_R
#raw 0xFE

#org @moveBack2
#raw STEP_D
#raw 0xFE

#org @talk8
= When's he giving free HMs to us\nscientists, eh?\pThat's what I'd like to know[.]

#org @talk9
= Well, Surf it up, buddy.\pIn the meantime, I'll be at my\ndesk, as always[.]